buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

apply plugin: 'org.spongepowered.mixin'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

mixin {
    config "crossstitch.mixins.json"
    add sourceSets.main, "crossstitch.refmap.json"
}

minecraft {
    mappings channel: "official", version: minecraft_version

    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "crossstitch" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', "crossstitch"
        }

        server {
            property 'forge.enabledGameTestNamespaces', "crossstitch"
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', "crossstitch"
        }

        data {
            workingDirectory project.file('run-data')

            args '--mod', "crossstitch", '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft("net.minecraftforge:forge:${minecraft_version}-${forge_version}")
}

tasks.named('processResources', ProcessResources) {
    var replaceProperties = [minecraft_version: minecraft_version, forge_version_range: forge_version_range,
                             mod_version: mod_version]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml']) {
        expand replaceProperties
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title"     : "crossstitch",
                    "Specification-Vendor"    : "Andrew Steinborn, Velocity Contributors",
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : "Andrew Steinborn, Velocity Contributors",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")])
    }

    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}